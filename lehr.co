# preoder of data on slide 7 of
# http://users.eecs.northwestern.edu/~haizhou/357/lec3.pdf
data =
  node: \V
  left:
    node: \H
    left:
      node: \V
      left:
        node: \H
        left:
          node: 1
        right:
          node: 6
      right:
        node: 2
    right:
      node: \V
      left:
        node: 7
      right:
        node: 5
  right:
    node: \H
    left:
      node: 3
    right:
      node: 4

# adapted from http://blog.pixelingene.com/2011/07/building-a-tree-diagram-in-d3-js/
<-! document.add-event-listener \DOMContentLoaded

tree = d3.layout.tree!size [500 500] .children ->
  []
    &push that if it.left
    &push that if it.right

nodes = tree.nodes data
links = tree.links nodes

layout-root = d3.select \body
  .append \svg:svg .attr width: 600 height: 600
  .append \svg:g
    .attr do
      class     : \container
      transform : 'translate(50, 50)'

link = d3.svg.diagonal!

layout-root.select-all \path.link
  .data links .enter!
  .append \svg:path
  .attr do
    class : \link
    d     : link

node-group = layout-root.select-all \g.node
  .data nodes .enter!
  .append \svg:g
  .attr do
    class     : \node
    transform : ({x, y}) -> "translate(#x, #y)"

circles = node-group.append \svg:circle
  .attr do
    class : \node-dot
    r     : 20

node-group.append \svg:text
  .attr class: \node-text
  .text -> it.node

# draw polish expr
expr-root = d3.select \body .append \p

# preprder walk
expr = []
visit = ->
  expr.push it.node
  visit that if it.left
  visit that if it.right

visit data

tokens = expr-root.select-all \span.token
  .data expr .enter!
  .append \span
    .attr class: \token
    .text -> it
    .on \mouseover (_, i) ->
      d3.select circles.0[i] .classed \hovered true
    .on \mouseout  (_, i) ->
      d3.select circles.0[i] .classed \hovered false

# highlight tokens from graph
node-group
  .on \mouseover (_, i) ->
    d3.select tokens.0[i] .classed \highlight true
  .on \mouseout  (_, i) ->
    d3.select tokens.0[i] .classed \highlight false

